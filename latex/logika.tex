\chapter{Logika prvog reda s induktivnim definicijama}\label{cha:logika-prvog-reda}
U ovom poglavlju predstavljamo glavne rezultate diplomskog rada: formalizaciju
logike prvog reda s induktivnim definicijama \(\mathit{FOL_{ID}}\)
te dokaznog sustava \(\mathit{LKID}\), koje je prvi uveo Brotherston~\cite{brotherston2005}.
Definicije, leme i dokazi u ovom poglavlju preuzete su iz Brotherstonove disertacije~\cite{brotherstonphd}.
Za općeniti uvod u logiku čitatelja upućujemo na knjigu \textit{Matematička logika}~\cite{vukovic}.

Prvo ćemo definirati sintaksu i semantiku logike \(\mathit{FOL_{ID}}\),
nakon čega ćemo definirati njene standardne modele.
Zatim ćemo prikazati dokazni sustav \(\mathit{LKID}\) te konačno dokazati dio adekvatnosti
sustava \(LKID\) s obzirom na standardnu semantiku,
što je ujedno i glavni rezultat ovog diplomskog rada.

Svaka definicija i lema u ovom poglavlju bit će popraćena svojom formalizacijom u Coqu.
Jedan je od ciljeva diplomskog rada prikazati primjene Coqa u matematici,
zbog čega leme nećemo dokazivati \enquote{na papiru},
već se dokaz svake leme može pronaći u repozitoriju rada.\footnote{\url{https://github.com/mihohren/diploma-thesis}}
Zainteresiranom čitatelju predlažemo interaktivni prolazak
kroz dokaze lema u nekoj od razvojnih okolina za Coq.

Prije no što krenemo na formalizaciju, valja prokomentirati odnos matematičkog i Coqovog vokabulara
što se tiče riječi \enquote{skup}.
U matematici pojam \enquote{skup} može imati dva značenja;
prvo se odnosi na skupove kao \textit{domene diskursa},
dok se drugo odnosi na skupove kao \textit{predikate}, odnosno podskupove domene diskursa.
Primjerice, skup prirodnih brojeva \(\mathbb{N}\) je domena diskursa kada je riječ o svim prirodnim brojevima
te zbog toga pišemo \(n \in \mathbb{N}\) umjesto \(\mathbb{N}(n)\).
S druge strane, skup svih parnih prirodnih brojeva \(E\) je podskup skupa \(\mathbb{N}\), a može se interpretirati
kao predikat parnosti na prirodnim brojevima te možemo pisati \(E(n)\) umjesto \(n \in E\).
U Coqu se skupovi kao domene diskursa formaliziraju tipovima sorte \coqset\footnote{Ili tipovima sorte \texttt{Type}
kada nam trebaju veliki tipovi, najčešće kao posljedica parametrizacije općenitih tipova.},
dok se skupovi kao predikati formaliziraju funkcijama iz domene diskursa u sortu \coqprop.
Na primjer, tip prirodnih brojeva \texttt{nat} je sorte \texttt{Set}, a predikat \texttt{Nat.Even} je tipa \texttt{nat\(\rightarrow\)Prop}.

\section{Sintaksa}\label{sec:sintaksa}
Kao i u svakom izlaganju logike, na početku je potrebno definirati sintaksu.
\begin{definition}\label{def:signatura}
  \textit{Signatura prvog reda s induktivnim predikatima} (kratko: \textit{signatura}), s oznakom \(\Sigma\),
  je skup simbola od kojih razlikujemo \textit{funkcijske}, \textit{obične predikatne}
  i \textit{induktivne predikatne} simbole.
  \textit{Mjesnost} simbola reprezentiramo funkcijom iz odgovarajućeg skupa simbola u skup \(\mathbb{N}\),
  a označujemo ju s \(|f|\) za funkcijske, odnosno s \(|P|\) za predikatne simbole.
\begin{minted}{coq}
Structure signature := {
    FuncS : Set;
    fun_ar : FuncS -> nat;
    PredS : Set;
    pred_ar : PredS -> nat;
    IndPredS : Set;
    indpred_ar : IndPredS -> nat;
  }.
\end{minted}
\end{definition}
\begin{example}
  Funkcijski simboli Peanove aritmetike su redom simbol nule \(o\) (mjesnosti~\(0\)),
  simbol sljedbenika \(s\) (mjesnosti \(1\))
  te simboli zbroja \(+\) i umnoška \(\cdot\) (mjesnosti \(2\)).
\begin{minted}{coq}
Inductive Func__PA :=
| PA_zero
| PA_succ
| PA_add
| PA_mult.
Definition fun_ar__PA (s : Func__PA) : nat :=
  match s with
  | PA_zero => 0
  | PA_succ => 1
  | PA_add  => 2
  | PA_mult => 2
  end.
\end{minted}
  \noindent Simbol jednakosti \(=\) (mjesnosti \(2\)) je jedini obični predikatni simbol
  Peanove aritmetike.
\begin{minted}{coq}
Inductive Pred__PA := PA_eq.
Definition pred_ar__PA (s : Pred__PA) : nat := 2.
\end{minted}
  \noindent Definiramo induktivne predikatne simbole
  \(\mathit{Nat}\), \(\mathit{Even}\) i \(\mathit{Odd}\) mjesnosti 1 koje ćemo
  interpretirati redom kao predikate \enquote{biti prirodan}, \enquote{biti paran} te
  \enquote{biti neparan}.
\begin{minted}{coq}
Inductive IndPred__PA :=
| PA_Nat
| PA_Even
| PA_Odd.
Definition indpred_ar__PA (s : IndPred__PA) : nat := 1.
\end{minted}
  \noindent Konačno, \textit{proširena Peanova signatura}, s oznakom \(\Sigma_{\mathit{PA}}\), je signatura za
  Peanovu aritmetiku prvog reda s prethodno definiranim induktivnim predikatnim simbolima.
\begin{minted}{coq}
Definition Σ__PA : signature
  := {|
    FuncS := Func__PA;
    fun_ar := fun_ar__PA;
    PredS := Pred__PA;
    pred_ar := pred_ar__PA;
    IndPredS := IndPred__PA;
    indpred_ar := indpred_ar__PA;
  |}.
\end{minted}
\end{example}
\noindent U ostatku poglavlja promatramo jednu proizvoljnu, ali fiksiranu signaturu \(\Sigma\).
Fiksiranje nekog proizvoljnog objekta je česta pojava u matematici,
prvenstveno zato što fiksirane argumente ne trebamo spominjati eksplicitno u kasnijim definicijama i iskazima.
Coq omogućuje fiksiranje naredbom \texttt{Context},
pod uvjetom da se korisnik nalazi u okolini \texttt{Section}.\footnote{\url{https://coq.inria.fr/doc/v8.18/refman/language/core/sections.html}}
Većina definicija i lema u ovom radu su napisane upravo unutar takvih okolina.

\begin{definition}\label{def:term}
  \textit{Varijabla} je prirodan broj. \textit{Skup svih terma} konstruiramo rekurzivno na način:
  \begin{enumerate}
  \item svaka varijabla je term;
  \item ako je \(f\) funkcijski simbol mjesnosti \(n\) te su \(t_{1}\), \ldots, \(t_{n}\) termi\footnote{Primijetimo, broj terma ovisi o mjesnosti funkcijskog simbola. U Coqovoj implementaciji ovog \enquote{konstruktora} možemo vidjeti da je on zavisnog tipa.},
    onda je \(f(t_{1}, \ldots, t_{n})\) također term.
  \end{enumerate}
\begin{minted}{coq}
Inductive term  : Set :=
| var_term : var -> term 
| TFunc : forall (f : FuncS Σ), vec term (fun_ar f) -> term.
\end{minted}
\end{definition}
\noindent Uobičajene prezentacije logike prvog reda za
skup varijabli uzimaju proizvoljni skup \(\mathcal{V}\),
no za formalizaciju je pogodniji skup prirodnih brojeva \(\mathbb{N}\).
Umjesto eksplicitne kvantifikacije po nekoj varijabli \(v\),
implicitno ćemo kvantificirati po varijabli \(0\).
Ovaj pristup kvantifikaciji\footnote{ili općenitije, vezivanju varijabli},
nazvan \enquote{de Bruijnovo indeksiranje}~\cite{debruijnamelessdummies},
bitno olakšava rad sa supstitucijama.
O samoj implementaciji de Bruijnovog indeksiranja više se može pročitati u knjizi
\textit{Types and Programming Languages}~\cite{pierce2002tapl}.
Za potrebe ovog rada koristili smo program \textit{Autosubst2}\footnote{\url{https://github.com/uds-psl/autosubst2}}~\cite{stark-phd,autosubst2}
za automatsko generiranje tipova terma i formula te pripadajućih funkcija supstitucija i pomoćnih lema.

Princip indukcije za \texttt{term} potrebno je ručno definirati.
Naime, induktivni tip \texttt{term} je \textit{ugniježđen} po konstruktoru \texttt{TFunc} zato što
se javlja omotan oko drugog induktivnog tipa\footnote{ovdje \texttt{vec}} kao argument.
Za ugniježđene induktivne tipove, Coq generira neprikladne principe indukcije jer ne zna
izraziti tvrdnju \enquote{predikat vrijedi za sve ugniježđene elemente}.
\begin{minted}{coq}
Lemma term_ind
     : forall P : term Σ -> Prop,
         (forall v, P (var_term v)) ->
         (forall f args, (forall st, V.In st args -> P st) ->
                         P (TFunc f args)) ->
       forall t : term Σ, P t.
\end{minted}

\begin{definition}
  \textit{Skup svih varijabli terma} \(t\), s oznakom \(\mathit{TV}(t)\), konstruiramo rekurzivno na način:
  \begin{enumerate}
  \item \( \mathit{TV}(v) \coloneq \{ v \} \) za varijablu \(v\) i
  \item \( \mathit{TV}(f(t_{1}, \ldots, t_{n})) \coloneq \bigcup_{1 \le i \le n} \mathit{TV}(t_{i}) \) za \(n\)-mjesni funkcijski simbol \(f\) i terme \(t_{1}, \ldots, t_{n}\).
  \end{enumerate}
\begin{minted}{coq}
Inductive TV : term -> var -> Prop :=
| TVVar : forall v, TV (var_term v) v
| TVFunc : forall f args v st,
    V.In st args -> TV st v -> TV (TFunc f args) v.
\end{minted}
\end{definition}

\begin{definition}\label{def:formula}
  \textit{Skup svih formula} konstruiramo rekurzivno na način:
  \begin{enumerate}
  \item ako je \(Q\) (obični ili induktivni) predikatni simbol mjesnosti \(n\) te su \(t_{1}\), \ldots, \(t_{n}\) termi,
    onda je \(Q(t_{1}, \ldots, t_{n})\) \textit{atomarna} formula;
  \item ako je \(\varphi\) formula, onda su \(\neg\varphi\) i \(\forall\varphi\) također formule;
  \item ako su \(\varphi\) i \(\psi\) formule, onda je \(\varphi \rightarrow \psi\) također formula.
  \end{enumerate}
\begin{minted}{coq}
Inductive formula : Set :=
| FPred (P : PredS Σ) : vec (term Σ) (pred_ar P) -> formula 
| FIndPred (P : IndPredS Σ) : vec (term Σ) (indpred_ar P) -> formula 
| FNeg : formula -> formula 
| FImp : formula -> formula -> formula 
| FAll : formula -> formula.
\end{minted}
  \noindent Ostale veznike definiramo kao sintaksne pokrate.
\begin{minted}{coq}
Definition FAnd (φ ψ : formula) : formula := FNeg (FImp φ (FNeg ψ)).
Definition FOr (φ ψ : formula) : formula := FImp (FNeg φ) ψ.
Definition FExist (φ : formula) : formula := FNeg (FAll (FNeg φ)).
\end{minted}
\end{definition}

\begin{example}
  U proširenoj Peanovoj signaturi, svojstvo \enquote{svaki prirodan broj je paran ili neparan}
  možemo izraziti formulom \( \forall x, \mathit{Nat}(x) \rightarrow \mathit{Even}(x) \lor \mathit{Odd}(x) \).
\begin{minted}{coq}
Definition every_nat_is_even_or_odd : formula Σ__PA :=
  FAll
    (FImp
       (FIndPred PA_Nat [var_term 0])
       (FOr
          (FIndPred PA_Even [var_term 0])
          (FIndPred PA_Odd  [var_term 0]))).
\end{minted}
  \noindent Ovdje vidimo učinak de Bruijnovog indeksiranja na zapis formula;
  umjesto eksplicitnog navođenja varijable \(x\) kod kvantifikacije,
  implicitno kvantificiramo po varijabli \(0\).
\end{example}

\begin{definition}
  \textit{Skup slobodnih varijabli formule} \(\varphi\), s oznakom \(\mathit{FV}(\varphi)\), konstruiramo rekurzivno na način:
  \begin{enumerate}
  \item \( \mathit{FV}(P(u_{1}, \ldots, u_{n})) \coloneq \bigcup_{1 \le i \le n} \mathit{TV}(u_{i}) \),
  \item \( \mathit{FV}(\neg\varphi) \coloneq \mathit{FV}(\varphi)\),
  \item \( \mathit{FV}(\varphi \rightarrow \psi) \coloneq \mathit{FV}(\varphi) \cup \mathit{FV}(\psi) \),
  \item \( \mathit{FV(\forall \varphi)} \coloneq \{ v \mid v + 1 \in \mathit{FV}(\varphi) \} \).
  \end{enumerate}
\begin{minted}{coq}
Inductive FV : formula -> var -> Prop :=
| FV_Pred : forall R args v st,
    V.In st args -> TV st v -> FV (FPred R args) v
| FV_IndPred : forall R args v st,
    V.In st args -> TV st v -> FV (FIndPred R args) v
| FV_Imp_l : forall F G v, FV F v -> FV (FImp F G) v
| FV_Imp_r : forall F G v, FV G v -> FV (FImp F G) v
| FV_Neg : forall F v, FV F v -> FV (FNeg F) v
| FV_All : forall F v, FV F (S v) -> FV (FAll F) v.
\end{minted}
\end{definition}

\begin{definition}
  \textit{Supstitucija} je svaka funkcija iz skupa \(\mathbb{N}\) u skup svih terma.
  Supstituciju \(\sigma\) možemo promatrati kao niz terma \(t_{0}, t_{1}, t_{2}, \ldots\).
  Tada je \textit{pomaknuta supstitucija}, s oznakom \(t \cdot \sigma\), supstitucija koja odgovara nizu \(t, t_{0}, t_{1}, t_{2}, \ldots\), za neki term \(t\).\\
  \noindent Domena supstitucije može se rekurzivno proširiti na skup svih terma i skup svih formula.
\begin{minted}{coq}
Fixpoint subst_term  (σ : var -> term) (t : term) : term :=
  match t with
  | var_term v => σ v
  | TFunc f args => TFunc f (V.map (subst_term σ) args)
  end.
\end{minted}
\begin{minted}{coq}
Fixpoint subst_formula
  (σ : var -> term Σ) (φ : formula )
  : formula  :=
  match φ return formula  with
  | FPred P args => FPred P (V.map (subst_term σ) args)
  | FIndPred P args => FIndPred P (V.map (subst_term σ) args)
  | FNeg ψ => FNeg (subst_formula σ ψ)
  | FImp ψ ξ => FImp (subst_formula σ ψ) (subst_formula σ ξ)
  | FAll ψ => FAll (subst_formula (up_term_term σ) ψ)
  end.
\end{minted}
  \noindent Ovdje funkcija \texttt{up\_term\_term} brine da supstitucija \(\sigma\) mijenja samo one varijable koje nisu vezane.
  \noindent Pišemo \(\varphi[\sigma]\) za primjenu supstitucije \(\sigma\) na formulu \(\varphi\).
  Često korištene supstitucije formula su
  supstitucija varijable \(x\) termom \(t\) u formuli \(\varphi\), s oznakom \(\varphi[t/x]\),
  te supstitucija svake varijable \(n\) u formuli \(\varphi\) varijablom \(n + 1\), s oznakom \(\varphi^{\uparrow}\).
  Iste notacije koristimo i za supstitucije na termima, listama terma i listama formula.
\end{definition}

Konačno, potrebno je definirati sintaksu za indukciju.
U Coqu su definicije induktivnih propozicija proizvoljne do na ograničenje pozitivnosti,
no radi jednostavnosti u \(\mathit{FOL_{ID}}\) su moguće samo
induktivne definicije s atomarnim formulama, a pišemo ih u stilu prirodne dedukcije:
\begin{prooftree}
  \AxiomC{\(  Q_{1}\mathbf{u}_{1}  \ldots   Q_{n}\mathbf{u}_{n}  \)}
  \AxiomC{\(  P_{1}\mathbf{v}_{1}  \ldots   P_{m}\mathbf{v}_{m}  \)}
  \BinaryInfC{\(P\mathbf{t}\)}
\end{prooftree}
\noindent Ovdje su \(Q_{1}, \ldots, Q_{n}\) obični predikatni simboli, \(P_{1}, \ldots, P_{m}\) i \(P\) su induktivni predikatni simboli,
a podebljani znakovi predstavljaju \(n\)-torke terma, gdje je \(n\) mjesnost odgovarajućeg predikata.

\begin{definition}
  Produkcija je uređena četvorka
  \begin{enumerate}
  \item liste parova običnih predikatnih simbola i \(n\)-torki terma odgovarajućih duljina,
  \item liste parova induktivnih predikatnih simbola i \(n\)-torki terma odgovarajućih duljina,
  \item induktivnog predikatnog simbola \(P\) mjesnosti \(m\) i
  \item \(m\)-torke terma.
  \end{enumerate}
\begin{minted}{coq}
Record production :=
  mkProd {
      preds : list {P: PredS Σ & vec (term Σ) (pred_ar P)};
      indpreds : list {P: IndPredS Σ & vec (term Σ) (indpred_ar P)};
      indcons : IndPredS Σ;
      indargs : vec (term Σ) (indpred_ar indcons);
    }.
\end{minted}
  Prvi i drugi član četvorke zovemo \textit{premisama}, a treći i četvrti \textit{konkluzijom}.
  U ostatku rada odabiremo neki podskup skupa svih produkcija koji zovemo
  \textit{skupom induktivnih definicija}, a označavamo s \(\Phi\).
\begin{minted}{coq}
Definition IndDefSet := production -> Prop.
\end{minted}
\end{definition}

U idućim primjerima definiramo produkcije za induktivne predikatne simbole u proširenoj Peanovoj signaturi.
\begin{example}\label{ex:productions-Nat}
  Za broj kažemo da je prirodan ako je \(0\) ili je sljedbenik nekog prirodnog broja.
  \begin{prooftree}
    \AxiomC{}
    \RightLabel{(\texttt{PA\_prod\_N\_zero})}
    \UnaryInfC{\( \mathit{Nat}(o) \)}
  \end{prooftree}
\begin{minted}{coq}
Definition PA_prod_N_zero : production Σ__PA.
  refine (mkProd nil nil PA_Nat _).
  refine [TFunc PA_zero []].
Defined.
\end{minted}
  \begin{prooftree}
    \AxiomC{\( \mathit{Nat}(x) \)}
    \RightLabel{(\texttt{PA\_prod\_N\_succ})}
    \UnaryInfC{\( \mathit{Nat}(s(x)) \)}
  \end{prooftree}
\begin{minted}{coq}
Definition PA_prod_N_succ : production Σ__PA.
  refine (mkProd nil _ PA_Nat _).
  - refine (cons _ nil). exists PA_Nat; refine [var_term 0].
  - refine [TFunc PA_succ [var_term 0]].
Defined.
\end{minted}
\end{example}

\begin{remark}
  U primjeru~\ref{ex:productions-Nat} vidimo alternativni način definiranja u Coqu.
  Korisnik umjesto definiranja Gallininih terma ulazi u način dokazivanja te
  naredbom \texttt{refine} postepeno konstruira (\enquote{profinjuje}) rezultatni term,
  ostavljajući \enquote{rupe} \texttt{\_} koje će popuniti kasnijim primjenama naredbe \texttt{refine}.
  Konačni rezultat možemo vidjeti naredbom \texttt{Print}.
\begin{verbatim}
Print PA_prod_N_succ.
\end{verbatim}
\begin{minted}[linenos=false]{coq}
PA_prod_N_succ =
{|
  preds := Datatypes.nil;
  indpreds := (PA_Nat; [var_term 0]) :: Datatypes.nil;
  indcons := PA_Nat;
  indargs := [TFunc PA_succ [var_term 0]]
|}
     : production
\end{minted}
\end{remark}

\begin{example}\label{ex:productions-Even-Odd}
  Za broj kažemo da je paran ako je \(0\) ili je sljedbenik nekog neparnog broja.
  \begin{prooftree}
    \AxiomC{}
    \RightLabel{(\texttt{PA\_prod\_E\_zero})}
    \UnaryInfC{\( \mathit{Even}(o) \)}
  \end{prooftree}
\begin{minted}{coq}
Definition PA_prod_E_zero : production Σ__PA.
  refine (mkProd nil nil PA_Even _).
  refine [ TFunc PA_zero []].
Defined.
\end{minted}
  \begin{prooftree}
    \AxiomC{\( \mathit{Odd}(x) \)}
    \RightLabel{(\texttt{PA\_prod\_E\_succ})}
    \UnaryInfC{\( \mathit{Even}(s(x)) \)}
  \end{prooftree}
\begin{minted}{coq}
Definition PA_prod_E_succ : production Σ__PA.
  refine (mkProd nil _ PA_Even _).
  - refine (cons _ nil). exists PA_Odd; refine [var_term 0].
  - refine [TFunc PA_succ [var_term 0]].
Defined.
\end{minted}
  \noindent Za broj kažemo da je neparan ako je sljedbenik nekog parnog broja.
  \begin{prooftree}
    \AxiomC{\( \mathit{Even}(x) \)}
    \RightLabel{(\texttt{PA\_prod\_O\_succ})}
    \UnaryInfC{\( \mathit{Odd}(s(x)) \)}
  \end{prooftree}
\begin{minted}{coq}
Definition PA_prod_O_succ : production Σ__PA.
  refine (mkProd nil _ PA_Odd _).
  - refine (cons _ nil). exists PA_Even; refine [var_term 0].
  - refine [TFunc PA_succ [var_term 0]].
Defined.
\end{minted}
  \noindent Skup prethodnih pet produkcija nazivamo
  skupom induktivnih definicija za proširenu Peanovu signaturu, s oznakom \(\Phi_{\mathit{PA}}\).
\begin{minted}{coq}
Inductive Φ__PA : production Σ__PA -> Prop :=
| ID_N_zero : Φ__PA PA_prod_N_zero
| ID_N_succ : Φ__PA PA_prod_N_succ
| ID_E_zero : Φ__PA PA_prod_E_zero
| ID_E_succ : Φ__PA PA_prod_E_succ
| ID_O_succ : Φ__PA PA_prod_O_succ.
\end{minted}
\end{example}


\section{Semantika}\label{sec:semantika}
\begin{definition}
  \textit{Struktura prvog reda} (kratko: \textit{struktura}) je uređena četvorka skupa \(M\) koji nazivamo \textit{nosačem}
  te interpretacij\=a funkcijskih, običnih predikatnih i induktivnih predikatnih simbola.
  Funkcijski simboli mjesnosti \(n\) interpretiraju se kao \(n\)-mjesne funkcije, a predikatni simboli mjesnosti \(n\) kao \(n\)-mjesne relacije na nosaču.
  Koristit ćemo ime nosača kao sinonim za čitavu strukturu,
  a interpretacije označavati s \(f^{M}\) odnosno \(P^{M}\).
\begin{minted}{coq}
Structure structure := {
    domain :> Set;
    interpF (f : FuncS Σ) : vec domain (fun_ar f) -> domain;
    interpP (P : PredS Σ) : vec domain (pred_ar P) -> Prop;
    interpIP (P : IndPredS Σ) : vec domain (indpred_ar P) -> Prop;
  }.
\end{minted}
\end{definition}

\begin{example}\label{ex:structure}
  Prije no što možemo definirati strukturu za Peanovu aritmetiku s induktivnim predikatima,
  odnosno strukturu za signaturu \(\Sigma_{\mathit{PA}}\) i skup induktivnih definicija \(\Phi_{\mathit{PA}}\),
  potrebno je definirati odgovarajuće interpretacije induktivnih predikatnih simbola u Coqu, u skladu s primjerima~\ref{ex:productions-Nat} i~\ref{ex:productions-Even-Odd}..
\begin{minted}{coq}
Inductive NAT : nat -> Prop :=
| NO : NAT 0
| NS : forall n, NAT n -> NAT (S n).

Inductive EVEN : nat -> Prop :=
| EO : EVEN 0
| ES : forall n, ODD n -> EVEN (S n)
with ODD : nat -> Prop :=
| OS : forall n, EVEN n -> ODD (S n).
\end{minted}
  \noindent Ovdje su predikati \texttt{EVEN} i \texttt{ODD} definirani mehanizmom simultane rekurzije.\\
  \noindent Konačno možemo definirati strukturu \(M_{\mathit{PA}}\) s nosačem \(\mathbb{N}\) te uobičajenom interpretacijom
  funkcijskih i običnih predikatnih simbola. Induktivne predikatne simbole
  \(\mathit{Nat}\), \(\mathit{Even}\) i \(\mathit{Odd}\)
  interpretiramo redom Coqovim predikatima \texttt{NAT}, \texttt{EVEN} i \texttt{ODD}.
\begin{minted}{coq}
Definition M__PA : structure Σ__PA.
  refine (Build_structure nat _ _ _).
  - intros f; destruct f.
    + intros. exact 0.
    + intros n. exact (S (V.hd n)).
    + intros xy. exact (V.hd xy + V.hd (V.tl xy)).
    + intros xy. exact (V.hd xy * V.hd (V.tl xy)).
  - intros P args; destruct P.
    exact (V.hd args = V.hd (V.tl args)).
  - intros P args; destruct P. 
    + exact (NAT (V.hd args)).
    + exact (EVEN (V.hd args)).
    + exact (ODD (V.hd args)).
Defined.
\end{minted}
\end{example}

\begin{remark}
  U primjeru~\ref{ex:structure} koristimo taktiku \texttt{exact} koja je specijalizacija taktike \texttt{refine},
  a koristimo ju u zadnjem koraku konstrukcije terma.
  Nadalje, taktika \texttt{intros x} služi uvođenju imena \texttt{x} u kontekst,
  a u našem je slučaju ekvivalentna taktici \texttt{refine (fun x => \_)}.
  Konačno, taktika \texttt{destruct} služi analizi po slučajevima, a u pozadini koristi \textit{pattern matching}.
  Kako su interpretacije funkcijskih simbola funkcije koje primaju vektore,
  koristimo Coqove funkcije \texttt{V.hd} i \texttt{V.tl} za dohvaćanje glave, odnosno repa vektora.
\end{remark}


\begin{definition}
  Neka je \(M\) proizvoljna struktura. \textit{Okolina} \(\rho\) za \(M\) je proizvoljna funkcija iz skupa varijabli (prirodnih brojeva) u nosač strukture.
\begin{minted}{coq}
Definition env := var -> M.
\end{minted}
  \noindent Okolina se može interpretirati kao niz \(d_{0}, d_{1}, d_{2}, \ldots\). Tada je \textit{pomaknuta okolina}, s oznakom \(d \cdot \rho\),
  niz \(d, d_{0}, d_{1}, d_{2}, \ldots\) za neki \(d \in M\).
  \noindent Proširenje domene okoline \(\rho\) na skup svih terma zovemo \textit{evaluacijom}.
\begin{minted}{coq}
Fixpoint eval (ρ : env) (t : term Σ) : M :=
  match t with
  | var_term x => ρ x
  | TFunc f args => interpF f (V.map (eval ρ) args)
  end.
\end{minted}
  Pišemo \(t^{\rho}\) za evaluaciju terma \(t\) u okolini \(\rho\).
  Istu notaciju koristimo i za evaluaciju \(n\)-torki terma.
\end{definition}

\begin{definition}
  Neka je \(M\) proizvoljna struktura te \(\rho\) okolina za \(M\).
  \textit{Istinitost} formule \(\varphi\) u okolini \(\rho\), s oznakom \(\rho \vDash \varphi\),
  definiramo rekurzivno na način:
  \begin{enumerate}
  \item ako je \(P\) (obični ili induktivni) predikatni simbol mjesnosti \(n\)
    te su \(u_{1}, \ldots, u_{n}\) termi,
    onda vrijedi \(\rho \vDash P(u_{1}, \ldots, u_{n})\) ako i samo ako vrijedi \(P^{M}(u_{1}^{\rho}, \ldots, u_{n}^{\rho})\),
  \item vrijedi \(\rho \vDash \neg \varphi \) ako i samo ako ne vrijedi \(\rho \vDash \varphi\) (što još pišemo \(\rho \not \vDash \varphi\)),
  \item vrijedi \(\rho \vDash \varphi \rightarrow \psi \) ako i samo ako vrijedi \(\rho \not \vDash \varphi\) ili \(\rho \vDash \psi\),
  \item vrijedi \(\rho \vDash \forall \varphi\) ako i samo ako za sve \(d \in M\) vrijedi \(d \cdot \rho \vDash \varphi\).
  \end{enumerate}
\begin{minted}{coq}
Fixpoint Sat (ρ : env M) (F : formula Σ) : Prop :=
  match F with
  | FPred P args => interpP P (V.map (eval ρ) args)
  | FIndPred P args => interpIP P (V.map (eval ρ) args)
  | FNeg G => ~ Sat ρ G
  | FImp F G => Sat ρ F -> Sat ρ G
  | FAll G => forall d, Sat (d .: ρ) G
  end.
\end{minted}
\end{definition}

\begin{example}
  Formula \(\forall x, \mathit{Nat}(x) \rightarrow \mathit{Even}(x) \lor \mathit{Odd}(x)\) je istinita u strukturi \(M_{\mathit{PA}}\)
  bez obzira na njenu okolinu.
\begin{minted}{coq}
Lemma every_nat_is_even_or_odd_Sat :
  forall (ρ : env M__PA), ρ ⊨ every_nat_is_even_or_odd.
\end{minted}
\end{example}

\begin{lemma}
  Neka su \(\varphi\), \(\sigma\), \(M\) i \(\rho\) redom
  proizvoljna formula, supstitucija, struktura i okolina za \(M\).
  Tada vrijedi \(\rho \vDash \varphi[\sigma]\) ako i samo ako vrijedi \( (t \mapsto t^{\rho}) \, \circ \, \sigma \vDash \varphi\).
\begin{minted}{coq}
Lemma strong_form_subst_sanity2 :
  forall (Σ : signature) (φ : formula Σ) (σ : var -> term Σ)
    (M : structure Σ) (ρ : env M),
    ρ ⊨ (subst_formula σ φ) <-> (σ >> eval ρ) ⊨ φ.
\end{minted}
  \noindent Kompoziciju supstitucije \(\sigma\) i evaluacije \(t \mapsto t^{\rho}\)
  možemo nazvati \textit{semantičkom} supstitucijom
  jer prvo provodi \textit{sintaksnu} supstituciju \(\sigma\) nakon čega provodi evaluaciju.
  Tada možemo neformalno reći da sintaksna i semantička supstitucija
  komutiraju pod relacijom istinitosti.
\end{lemma}

\section{Standardni modeli}\label{sec:standardni-modeli}
Želimo ograničiti semantička razmatranja na samo one strukture koje \enquote{imaju smisla} za induktivne predikate.
Prisjetimo se, predikatni simbol \(P\) mjesnosti \(n\) interpretira se na strukturi \(M\)  podskupom skupa \(M^{n}\).
Indukciju smatramo dokazivanjem \textup{u razinama} pa ima smisla promatrati \textit{razine interpretacije} induktivnog predikata,
gdje je nulta razina prazan skup,
a svaku iduću razinu konstruiramo pomoću produkcija induktivnog skupa definicija i prethodnih razina.
Tako je prva razina onaj podskup kojeg možemo dobiti najviše jednom primjenom produkcija,
druga je razina onaj podskup kojeg možemo dobiti pomoću najviše dviju primjena produkcija, i tako dalje.
Na taj se način, korak po korak, gradi \textit{smislena} interpretacija induktivnih predikata.
Zbog mogućih međuovisnosti induktivnih predikata,
razine interpretacije moramo definirati simultano.
Ovaj odjeljak posvećujemo formalizaciji ovih pojmova.

\begin{definition}
  Neka je \(M\) proizvoljna struktura te neka je \(pr\) proizvoljna produkcija induktivnog skupa definicija \(\Phi\), primjerice:
  \begin{prooftree}
  \AxiomC{\(  Q_{1}\mathbf{u}_{1}  \ldots   Q_{n}\mathbf{u}_{n}  \)}
  \AxiomC{\(  P_{1}\mathbf{v}_{1}  \ldots   P_{m}\mathbf{v}_{m}  \)}
  \RightLabel{.}
  \BinaryInfC{\(P \, \mathbf{t}\)}
\end{prooftree}
\noindent Neka je \(I\) proizvoljna interpretacija induktivnih predikatnih simbola.
Tada definiramo \(\varphi_{pr}(I)\) kao skup svih \(|P|\)-torki \(\mathbf{d}\) elemenata nosača \(M\) za koje postoji okolina \(\rho\) za \(M\) takva da:
\begin{itemize}
\item za sve \( i \in \{ 1, \ldots, n \} \) vrijedi \( \mathbf{u}_{i}^{\rho} \in Q_{i}^{M} \),
\item za sve \( j \in \{ 1, \ldots, m \} \) vrijedi \( \mathbf{v}_{j}^{\rho} \in f(P_{j}) \) i
\item \(\mathbf{d} = \mathbf{t}^{\rho}\).
\end{itemize}
\begin{minted}{coq}
Definition φ_pr
  (pr : production Σ)
  (interp : InterpInd)
  (ds : vec M (indpred_ar (indcons pr)))
  : Prop :=
      exists (ρ : env M),
      (forall Q us, List.In (Q; us) (preds pr) ->
               interpP Q (V.map (eval ρ) us)) /\
        (forall P ts, List.In (P; ts) (indpreds pr) ->
                  interp P (V.map (eval ρ) ts)) /\
        ds = V.map (eval ρ) (indargs pr).
\end{minted}
\noindent Operator \(\varphi_{pr}\) je formalizacija ideje primjene produkcije.
Nadalje, potrebno je definirati operator koji će uzeti u obzir \textit{sve} produkcije koje se odnose na \(P\).
Definiramo \(\varphi_{P}(f)\) kao uniju svih \(\varphi_{pr^{\prime}}(f)\)
gdje je \(pr^{\prime}\) produkcija u kojoj se \(P\) javlja u konkluziji.
\begin{minted}{coq}
Definition φ_P
  (P : IndPredS Σ)
  (interp : InterpInd)
  : vec M (indpred_ar P) -> Prop.
  refine (fun ds => _).
  refine (@ex (production Σ) (fun pr => _)).
  refine (@ex (P = indcons pr /\ Φ pr) (fun '(conj Heq HΦ) => _)).
  rewrite Heq in ds.
  exact (φ_pr pr interp ds).
Defined.
\end{minted}
\noindent Konačno, definiramo \textit{operator skupa definicija} \(\varphi_{\Phi}\) kao preslikavanje
koje svakom induktivnom predikatnom simbolu \(P\) pridružuje skup \(\varphi_{P}(f)\).
\begin{minted}{coq}
Definition φ_Φ (interp : InterpInd) : InterpInd :=
  fun P => φ_P P interp.
\end{minted}
\noindent Operator \(\varphi_{\Phi}\) omogućuje simultanu primjenu produkcija.
\end{definition}

\begin{remark}\label{remark:phi-phi-notation}
  Kako je funkcija \(f\) bila uvedena na samom početku prethodne definicije,
  u stvari definicija operatora \(\varphi_{\Phi}\) glasi
  \(
    \varphi_{\Phi}(f)(P) \coloneq \varphi_{P}(f)
  \).
\end{remark}

\begin{example}\label{ex:operator-phi-phi-PA}
  Ilustrirajmo operator \(\varphi_{\Phi}\) na strukturi \(M_{\mathit{PA}}\) i
  skupu induktivnih definicija \(\Phi_{\mathit{PA}}\).
  Prisjetimo se, u proširenoj Peanovoj signaturi imamo tri induktivna predikatna simbola;
  \(\mathit{Nat}\), \(\mathit{Even}\) i \(\mathit{Odd}\), i svi su oni jednomjesni.
  Dakle, domena i kodomena operatora \(\varphi_{\Phi}\) u ovom primjeru je skup \(\mathcal{P(\mathbb{N})}^{3}\),
  gdje prva, druga i treća projekcija odgovaraju redom interpretacijama predikata
  \(\mathit{Nat}\), \(\mathit{Even}\) i \(\mathit{Odd}\). Nadalje, vrijedi:
  \[
    \varphi_{\Phi_{\mathit{PA}}, M_{\mathit{PA}}}(N, E, O) =
    ( \{ 0 \} \cup \{ n + 1 \mid n \in N \},
    \{ 0 \} \cup \{ o + 1 \mid o \in O \},
    \{ e + 1 \mid e \in E \} ).
  \]
  \noindent Prema notaciji u prethodnoj napomeni,
  funkcija \(f\) je ovdje preslikavanje
  \[(\mathit{Nat} \mapsto \mathit{N}, \mathit{Even} \mapsto \mathit{E}, \mathit{Odd} \mapsto \mathit{O})\]
  koje možemo reprezentirati uređenom trojkom \((N, E, O)\).
  Indeksiranje induktivnim predikatnim simbolom odgovara prethodno
  navedenim projekcijama pa vrijedi primjerice:
  \[
    \varphi_{\Phi_{\mathit{PA}}, M_{\mathit{PA}}}(N, E, O)(\mathit{Nat}) = \{ 0 \} \cup \{ n + 1 \mid n \in N \}.
  \]
\end{example}

\begin{definition}
  Neka su \(U\) i \(V\) proizvoljne interpretacije induktivnih predikatnih simbola.
  Kažemo da je interpretacija \(U\) \textit{podinterpretacija} interpretacije \(V\)
  i pišemo \(U \sqsubseteq V\)
  ako za svaki induktivni predikatni simbol \(P\) vrijedi \(U(P) \subseteq V(P)\).
\begin{minted}{coq}
Definition subinterp (U V : InterpInd) := forall P v, U P v -> V P v.
Notation "U ⊑ V" := (subinterp U V) (at level 11).
\end{minted}
\end{definition}

\begin{proposition}
  Operator \(\varphi_{\Phi}\) je monoton.
\begin{minted}{coq}
Definition monotone (F : InterpInd -> InterpInd) :=
  forall U V : InterpInd, U ⊑ V -> F U ⊑ F V.
  
Proposition φ_Φ_monotone : monotone φ_Φ.
\end{minted}
\end{proposition}

\begin{definition}
  Neka je \(M\) proizvoljna struktura.
  Definiramo \textit{aproksimaciju} skupa induktivnih definicija \(\Phi\) razine \(\alpha \in \mathbb{N}\), s oznakom \(\varphi_{\Phi}^{\alpha}\), rekurzivno na način:
  \begin{enumerate}
  \item \( \varphi_{\Phi}^{0}(P) \coloneq \varnothing \) i
  \item \( \varphi_{\Phi}^{\alpha + 1} \coloneq \varphi_{\Phi}(\varphi_{\Phi}^{\alpha}) \).
  \end{enumerate}
  \noindent Neformalno možemo reći da operator \(\varphi_{\Phi}\) \textit{poboljšava} aproksimaciju prethodne razine.
\begin{minted}{coq}
Fixpoint φ_Φ_n (α : nat) : InterpInd :=
  match α with
  | 0 => fun _ _ => False
  | S α => φ_Φ (φ_Φ_n α)
  end.
\end{minted}
  \noindent Tada je \textit{aproksimant} induktivnog predikatnog simbola \(P\) razine \(\alpha\) upravo \(\varphi_{\Phi}^{\alpha}(P)\).
\end{definition}

\begin{remark}
  Brotherston je definirao aproksimaciju razine \(\alpha\) za pojedini induktivni predikatni simbol
  kao uniju poboljšanih aproksimacija svih nižih razina.
  Takva je definicija ekvivalentna našoj.
\end{remark}

\begin{lemma}\label{lemma:ekvivalentnost-definicija-aproksimacija}
  Za svaki prirodni broj \(\alpha\) i induktivni predikatni simbol \(P\) vrijedi
  \[
    \varphi_{\Phi}^{\alpha}(P) = \bigcup_{\beta < \alpha} \varphi_{\Phi}(\varphi_{\Phi}^{\beta}(P)).
  \]
\begin{minted}{coq}
Lemma φ_Φ_n_characterization : forall α P v,
    φ_Φ_n α P v <-> exists β, β < α /\ φ_Φ (φ_Φ_n β) P v.
\end{minted}
\end{lemma}

\begin{example}
  Za svaki prirodni broj \(\alpha\), a u skladu s primjerom~\ref{ex:operator-phi-phi-PA},
  aproksimant razine \(\alpha\) induktivnog predikatnog simbola \(\mathit{Nat}\)
  je skup svih prirodnih brojeva strogo manjih od \(\alpha\), odnosno vrijedi:
  \[
    \varphi_{\Phi_{\mathit{PA}}, M_{\mathit{PA}}}^{\alpha}(\mathit{Nat}) = \{ 0, 1, \ldots, \alpha - 1 \}.
  \]
\begin{minted}{coq}
Lemma approximants_of_PA_Nat : forall α n,
    φ_Φ_n Φ__PA M__PA α PA_Nat [n] <-> n < α.
\end{minted}
\end{example}

\begin{definition}
  \textit{Aproksimacija razine} \(\omega\), s oznakom \(\varphi_{\Phi}^{\omega}\), je (za svaki pojedini predikatni simbol)
  unija aproksimacija razina manjih od \(\omega\).
\begin{minted}{coq}
Definition φ_Φ_ω : InterpInd := fun P v => exists α, φ_Φ_n α P v.
\end{minted}
\end{definition}

\begin{example}
  Aproksimant razine \(\omega\) induktivnog predikatnog simbola \(\mathit{Nat}\)
  je cijeli skup prirodnih brojeva \(\mathbb{N}\).
\begin{minted}{coq}
Lemma NAT_φ_Φ_ω: forall n, φ_Φ_ω Φ__PA M__PA PA_Nat [n].
\end{minted}
\end{example}

\begin{definition}
  Neka je \(U\) proizvoljna interpretacija induktivnih predikatnih simbola.
  Kažemo da je interpretacija \(U\) \textit{prefiksna točka} ako
  je poboljšanje interpretacije \(U\) podinterpretacija od \(U\),
  odnosno vrijedi \(\varphi_{\Phi}(U) \sqsubseteq U\).
  Za interpretaciju induktivnih predikatnih simbola \(V\) kažemo da je
  \textit{najmanja prefiksna točka} ako je \(V\) prefiksna točka 
  te je podinterpretacija svake druge prefiksne točke.
\begin{minted}{coq}
Definition prefixed (U : InterpInd) := φ_Φ U ⊑ U.
Definition least (P : InterpInd -> Prop) (U : InterpInd) :=
  P U /\ (forall V, P V -> U ⊑ V).
\end{minted}
\end{definition}

\begin{lemma}\label{lemma:omega-least-prefixed}
  Aproksimacija razine \(\omega\) je najmanja prefiksna točka operatora \(\varphi_{\Phi}\).
\begin{minted}{coq}
Lemma φ_Φ_ω_least_prefixed : least prefixed φ_Φ_ω.
\end{minted}
\end{lemma}

\begin{definition}
  Kažemo da je struktura \(M\) \textit{standardni model} za \(\Phi\) ako
  interpretira svaki induktivni predikatni simbol njegovim aproksimantom razine \(\omega\).
\begin{minted}{coq}
Definition standard_model (Φ: IndDefSet Σ) (M : structure Σ) : Prop :=
  forall (P : IndPredS Σ) ts, interpIP P ts <-> φ_Φ_ω Φ M P ts.
\end{minted}
\end{definition}
\noindent Prema lemi~\ref{lemma:omega-least-prefixed}, standardni model je
najmanja struktura koja ima smisla za zadani skup induktivnih definicija.

\begin{proposition}
  Struktura \(M_{\mathit{PA}}\) je standardni model za skup induktivnih definicija \(\Phi_{\mathit{PA}}\).
\begin{minted}{coq}
Lemma standard_model__PA : standard_model Φ__PA M__PA.
\end{minted}
\end{proposition}


%%% Local Variables:
%%% mode: LaTeX
%%% TeX-master: "master"
%%% End:
